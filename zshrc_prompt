#################################
# Prompt
setopt prompt_subst prompt_percent transient_rprompt
autoload colors
colors

# create prompt
autoload -Uz vcs_info
zstyle ':vcs_info:*' formats '(%{%F{green}%}%s%{%f%})-[%{%F{blue}%}%b%{%f%}]'
zstyle ':vcs_info:*' actionformats '(%{%F{green}%}%s%{%f%})-[%{%F{blue}%}%b%{%f%}|%{%F{red}%}%a%{%f%}]'

prompt_fullpath="%{%B%F{magenta}%}%d%{%b%f%}"
prompt_userhost="%{%B%}%{%F{cyan}%}%n@%m%{%f%}%{%b%}"
prompt_status="%{%B%(?.%F{green}.%F{red})%}%?%{%f%b%}"
prompt_date="%{%B%F{red}%}%D{%Y/%m/%d %H:%M}%{%b%f%}"

prompt_first="(${prompt_userhost})-(${prompt_status})-[${prompt_fullpath}]"
prompt_second="%# "

update_prompt() {
    PROMPT=$'\n'"${prompt_first}"$'\n'"${prompt_second}"
    RPROMPT=""

    LANG=C vcs_info >&/dev/null
    if [ -n "$vcs_info_msg_0_" ]; then
        RPROMPT="${vcs_info_msg_0_}"
    fi
}

## コマンド実行前に呼び出されるフック。
precmd_functions=($precmd_functions update_prompt)
